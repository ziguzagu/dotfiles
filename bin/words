#!/usr/bin/env perl
use strict;
use warnings;
use utf8;

use Getopt::Long qw(:config posix_default no_ignore_case gnu_compat);
use Pod::Usage;
use List::Util qw(uniqstr);

GetOptions(\my %opt, qw(length|l=i help|h))
    or pod2usage(1);
$opt{help} && pod2usage(0);

$opt{length} //= 4;

my $capture = do { local $/; <STDIN> };
# split roughly
$capture =~ tr/'"()\\[]/ /;
my @words = split /\s+/, $capture;
# scrub
for (@words) {
    s/\:\d+\z//; # remove line number
    s/\Agit://;  # remove "git:<branch>" of prompt
    s/\W+\z//;   # remove trailing non-word letters
}
# filter
@words =
    grep { /\A\~?[\w\/\.]+\S+\z/ }
    grep { length >= $opt{length} }
    uniqstr @words;

print join "\n", @words, '';

__END__

=pod

=head1 NAME

words - find the words for command options

=head1 SYNOPSIS

  $ tmux capture-pane -p | words

=head1 OPTIONS

=over 4

=item B<-l|--length <number>>

Minimum word length. Default is 4.

=item B<-h, --help>

Print help document now you see.

=back

=cut
